package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.expression.* ;
import yal.arbre.instruction.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.tabledessymboles.*;
import java_cup.runtime.*;

action code {: 
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.PLUS), "+") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MOINS), "-") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MULT), "*") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIV), "/") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PAROUV), "(") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PARFER), ")") ;
        lesTerminaux.put(new Integer(CodesLexicaux.SUP), ">") ;
        lesTerminaux.put(new Integer(CodesLexicaux.INF), "<") ;
        lesTerminaux.put(new Integer(CodesLexicaux.EGALEGAL), "==") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIFF), "!=") ;
        lesTerminaux.put(new Integer(CodesLexicaux.ET), "et") ;
        lesTerminaux.put(new Integer(CodesLexicaux.OU), "ou") ;
        lesTerminaux.put(new Integer(CodesLexicaux.NON), "non") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1) + " | ") ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1) + " => ") ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CONSTANTEINT, "" + s.value) ;
                lesTerminaux.put(CodesLexicaux.CONSTANTEBOOL, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PLUS, MOINS, MULT, DIV, PAROUV, PARFER, POINTVIRGULE;
terminal Symbol    SUP, INF, EGALEGAL, DIFF, ET, OU, NON, EGAL ;
terminal String    PROGRAMME, DEBUT, FIN ;
terminal String    IDF, LIRE, TANTQUE, REPETER, FINTANTQUE  ;
terminal String    CONSTANTEINT, CONSTANTEBOOL , CONSTANTECHAINE ;
terminal String	   SI, ALORS, SINON, FSI;
terminal String    FONCTION, RETOURNE ;

terminal String    ENTIER, ECRIRE ;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG ;
non terminal Instruction INSTR ;
non terminal Expression EXPR, CSTE ;
non terminal Instruction AFFECT, PRINT, READ, BOUCLE, CONDITION, ELSE;
non terminal BlocDInstructions LINSTR;
non terminal BlocDInstructions LDECL;

non terminal ArbreAbstrait DECL_FONCTION, DECL_VAR, DECLARATION ;
non terminal Symbol PARAMETRES;

precedence left OU ;
precedence left ET ;
precedence left EGALEGAL, DIFF;
precedence left INF, SUP ;
precedence left PLUS, MOINS;
precedence left MULT, DIV ;
precedence left NON ;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME IDF DEBUT LDECL:d LINSTR:e FIN
				{:
				    BlocDInstructions bloc = new BlocDInstructions(dleft+1) ;
				    bloc.ajouter(e) ;
				    bloc.ajouter(d) ;
				    RESULT = bloc;
				:}
        |
                PROGRAMME IDF DEBUT LINSTR:e FIN
                {: RESULT = e; :}
				;


LINSTR    ::=     INSTR:e 
                {:  BlocDInstructions bloc = new BlocDInstructions(eleft+1) ;
                	bloc.ajouter(e) ;
                	RESULT = bloc ; :}
        |
        		LINSTR:li INSTR:e
                {: 	li.ajouter(e) ;
                	RESULT = li ; :}
                ;
                
LDECL ::= 	DECLARATION:d
            {:  BlocDInstructions bloc = new BlocDInstructions(dleft+1) ;
                bloc.ajouter(d) ;
                RESULT = bloc ; :}
	  |
	  		LDECL:ld DECLARATION:d
	  		{: 	ld.ajouter(d) ;
                RESULT = ld ; :}
            ;


DECLARATION ::= DECL_VAR:v
                {: RESULT = v ; :}
        |
                DECL_FONCTION:f
                {: RESULT = f ; :}
				;
				
DECL_VAR ::= 	ENTIER IDF:i POINTVIRGULE
				{:
				    TDS.getInstance().ajouter(new Entree(i), new Symbole(),ileft+1);
				    RESULT = new DeclVar(i, ileft) ;
				:}
 				;
 				
AFFECT ::= 		IDF:i EGAL EXPR:e
				{:  RESULT = new Affectation(i,e,eleft+1);  :}
				;
				    
EXPR    ::=     EXPR:g  PLUS  EXPR:d
                {: RESULT = new Plus(g, d) ; :}
        | 
                EXPR:g  MOINS  EXPR:d
                {: RESULT = new Moins(g, d); :}
        |                
                EXPR:g  MULT  EXPR:d
                {: RESULT = new Mult(g, d); :}
        |                
                EXPR:g  DIV  EXPR:d
                {: RESULT = new Div(g, d); :}
        |       
                EXPR:g  INF  EXPR:d
                {: RESULT = new Inferieur(g, d); :}
        |       
                EXPR:g  SUP  EXPR:d
                {: RESULT = new Superieur(g, d); :}
        |              
                EXPR:g  EGALEGAL  EXPR:d
                {: RESULT = new Egal(g, d); :}
        |       
                EXPR:g  DIFF  EXPR:d
                {: RESULT = new Different(g, d); :}
        |                      
                MOINS  EXPR:e  
                {: RESULT = new MoinsUnaire(e); :}
        |      
                EXPR:g  ET  EXPR:d
                {: RESULT = new EtLogique(g, d); :}
        |      
                EXPR:g  OU  EXPR:d
                {: RESULT = new OuLogique(g, d); :}
        |      
                NON EXPR:e
                {: RESULT = new NonLogique(e); :}
        |
                CSTE:c
                {: RESULT = c ; :}
        |
                IDF:i
                {: RESULT = new IDF(i, ileft+1) ; :}
        |
                PAROUV  EXPR:e  PARFER
                {: RESULT = e ; :}
        |
                IDF:i PAROUV PARFER
                {: RESULT = new AppelFunc(i, ileft + 1) ; :}
				;
				
INSTR ::=       PRINT:p POINTVIRGULE
				{: RESULT = p ; :}
        |
                AFFECT:a POINTVIRGULE
                {: RESULT = a ; :}
        |
        		READ:r POINTVIRGULE
        		{: RESULT = r ; :}
        |
        		BOUCLE:b
        		{: RESULT = b ; :}
        |
        		CONDITION:c
        		{: RESULT = c ; :}
        |
                RETOURNE EXPR:e POINTVIRGULE
                {: RESULT = new Retourne(e, eleft+1) ; :}
                ;

READ ::= 	LIRE IDF:i
			{: RESULT = new Lire(i, ileft + 1) ; :}
			;

PRINT ::=      ECRIRE EXPR:e
                {:  RESULT = new EcrireExpr(e, eleft + 1) ; :}
        |
               ECRIRE CONSTANTECHAINE:c
               {:  RESULT = new EcrireChaine(c, cleft + 1) ; :}
        ;

BOUCLE ::= 	TANTQUE EXPR:e REPETER LINSTR:l FINTANTQUE
			{:  RESULT = new Boucle(e, l, eleft + 1) ; :}
			;
			
CONDITION ::= 	SI EXPR:cond ALORS LINSTR:si SINON LINSTR:sinon FSI
				{: RESULT = new Condition(cond, si, sinon, condleft + 1) ; :}
        |
                SI EXPR:cond ALORS LINSTR:si FSI
                {: RESULT = new Condition(cond, si, null, condleft + 1) ; :}
        |
                SI EXPR:cond ALORS  FSI
                {: RESULT = new Condition(cond, null, null, condleft + 1) ; :}
        |
        		SI EXPR:cond ALORS SINON LINSTR:sinon FSI
				{: RESULT = new Condition(cond, sinon, condleft + 1) ; :}
				
		|
		        SI EXPR:cond ALORS SINON FSI
				{: RESULT = new Condition(cond, null, null, condleft + 1) ; :}
				;
			
CSTE    ::=     CONSTANTEINT:e 
                {: RESULT = new ConstanteEntiere(e, eleft+1) ; :}
        |       CONSTANTEBOOL:e 
                {: RESULT = new ConstanteBool(e, eleft+1) ; :}
                ;

/* FONCTIONS */

DECL_FONCTION ::=  FONCTION IDF:i PAROUV PARFER DEBUT LINSTR:li FIN
                   {:
                     TDS.getInstance().ajouter(new Entree("func_" + i), new Symbole(), ileft+1) ;
                     RESULT = new Fonction(i, li, lileft+1) ;
                   :}
/*
        |
                   FONCTION IDF PARAMETRES DEBUT LDECL:ld LINSTR:li FIN
                   {:  :}
*/
        ;

PARAMETRES ::=  PAROUV PARFER
                {:  :}
        ;